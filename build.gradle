buildscript {
    ext {
        springBootVersion = "1.5.17.RELEASE"
        springCloudVersion = "Dalston.SR5"
        lombokVersion = "1.16.20"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
    }
}

plugins {
    id "net.ltgt.apt" version "0.10"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'org.springframework.boot'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

//---------------------------------
// Configurations
//---------------------------------
configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestCompile.extendsFrom testRuntime

    // Providing a separate configuration for Lombok itself
    lombok
}

sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output

        // You can add other directories to the classpath like this:
        // Use "java" if you don't use Scala as a programming language
        groovy.srcDir file("src/integration/groovy")
        java.srcDir file("src/integration/java")

        resources.srcDir file('src/integration/resources')
    }

    // test {
    //     resources.srcDir file('src/integration/resources')
    // }
}

ext["spock.version"] = "1.1-groovy-2.4"

dependencyManagement {

    imports {
        mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
        mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
    }
}

dependencies {

    compile("org.projectlombok:lombok:${lombokVersion}")
    apt("org.projectlombok:lombok:${lombokVersion}")

    compile("org.springframework.boot:spring-boot-starter-data-mongodb:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion")
    compile("org.springframework.boot:spring-boot-starter-thymeleaf:$springBootVersion")
    compile("org.springframework.boot:spring-boot-configuration-processor:$springBootVersion")
    compile("org.springframework.cloud:spring-cloud-starter-hystrix:1.4.6.RELEASE")
    compile("org.springframework.cloud:spring-cloud-starter-ribbon:1.4.6.RELEASE")

    compile("org.apache.commons:commons-math3:3.6.1")
    compile("com.google.guava:guava:24.0-jre")
    compile("javax.ws.rs:javax.ws.rs-api:2.1")

    compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jdk8")

    compile("org.mock-server:mockserver-netty:3.11")
    compile("io.rest-assured:rest-assured:3.0.5")

    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.spockframework:spock-core")
    testCompile("org.spockframework:spock-spring")

    integrationTestCompile("io.rest-assured:rest-assured:3.0.7")
    integrationTestCompile("org.mock-server:mockserver-netty:3.11")
    integrationTestCompile("de.flapdoodle.embed:de.flapdoodle.embed.mongo")
}

processResources {
    filesMatching('*.yml') {
        expand(project.properties)
    }
    outputs.upToDateWhen { false }
}

bootRun {
  // Pass on all properties like -DSPRING_PROFILES_ACTIVE=prod
  environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: "local"
}

test {
  // Pass on all properties like -DSPRING_PROFILES_ACTIVE=production
  environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: "test"
}

task debug(dependsOn: bootRun) {
    gradle.taskGraph.whenReady { graph ->
        if (graph.hasTask(debug)) {
            bootRun {
                debug = true
            }
        }
    }
}